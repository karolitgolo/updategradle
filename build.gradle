plugins {
    id 'org.unbroken-dome.test-sets' version '1.4.2'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
}

group projectGroup
version projectVersion

apply plugin: 'java'

Properties env = new Properties()
env.load(new FileInputStream("env.properties"))
sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration-test' }
    functionalTest { dirName = 'functional-test' }
}

test.outputs.upToDateWhen {false}
integrationTest.outputs.upToDateWhen {false}
functionalTest.outputs.upToDateWhen {false}

dependencies {
    compile group: 'commons-net', name: 'commons-net', version: '3.6'
    compile group: 'org.apache.maven', name: 'maven-artifact', version: '3.5.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile gradleTestKit()
    compile gradleApi()
}

gradlePlugin {
    plugins {
        "${projectArtifact}Plugin" {
            id = pluginId
            implementationClass = pluginClass
        }
    }
}

sourceSets {
    functionalTest {
        java {
            srcDir 'src/main/java'
        }
   }
   integrationTest {
        java {
            srcDir 'src/main/java'
        }
    }
}

bintray {
    user = bintrayUser
    key = env.getProperty("BINTRAY_APIKEY")
    publish = true
    pkg {
        repo = bintrayRepository
        name = projectArtifact
        desc = description
        userOrg = bintrayUser
        licenses = ((String) projectLicenses).split(";")
        vcsUrl = scm
        labels = ((String) projectTags).split(";")
        publicDownloadNumbers = true
        websiteUrl = url
    }
    publications = ['pluginPublication']
}

pluginBundle {
    website = url
    vcsUrl = scm
    tags = ((String) projectTags).split(";")
    plugins {
        "${projectArtifact}Plugin" {
            displayName = displayName
            description = description
            id = pluginId
        }
    }
}

publishing {
    publications {
        pluginPublication(MavenPublication) {
            from components.java
            groupId projectGroup
            artifactId projectArtifact
            version projectVersion
        }
    }
    repositories {
        mavenLocal()
    }
}

bintrayUpload.dependsOn test

test.dependsOn clean
test.dependsOn functionalTest
test.dependsOn integrationTest

test.mustRunAfter clean
functionalTest.mustRunAfter integrationTest